#compdef ansible-galaxy
# ------------------------------------------------------------------------------
# Copyright (c) 2011 Github zsh-users - http://github.com/zsh-users
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#     * Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in the
#       documentation and/or other materials provided with the distribution.
#     * Neither the name of the zsh-users nor the
#       names of its contributors may be used to endorse or promote products
#       derived from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
# ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL ZSH-USERS BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
# ------------------------------------------------------------------------------
# Description
# -----------
#
#  Completion script for ansible-galaxy v1.8.4 (http://ansible.org)
#
# ------------------------------------------------------------------------------
# Authors
# -------
#
#  * Romain Bossart (https://github.com/bosr)
#
# ------------------------------------------------------------------------------
#
# Note: I tried to use `_arguments --`, but the output of `ansible --help`
# is not parsed entirely correctly, and anyway no modules or host would available.
#

# ansible-galaxy zsh completion
#
_ansible-galaxy ()
{
  local context curcontext="$curcontext" state line
  typeset -A opt_args

  _arguments -C \
    "1:command:(init info install list remove)" \
    "*::option:->option"

  case $state in
    option)
      case $line[1] in
        init)
          _arguments \
            "(-h --help)"{-h,--help}"[help message]" \
            "(-p --init-path)"{-p,--init-path}"[INIT_PATH path in which the skeleton role will be created (default=./)]:init path:(./)" \
            "(--offline)--offline[Don't query the galaxy API when creating roles]" \
            "(-s --server)"{-s,--server}"[API_SERVER The API server destination]:api server:(http://apiserver)" \
            "(-f --force)"{-f,--force}"[Force overwriting an existing role]" \
            ":role name:(ROLE)"
          ;;
        info)
          _arguments \
            "(-h --help)"{-h,--help}"[help message]" \
            "(-p --roles-path)"{-p,--roles-path}"[ROLES_PATH The path to the directory containing your roles (default: from ansible.cfg)]:roles path:_files -/" \
            "(-s --server)"{-s,--server}"[API_SERVER The API server destination]:api server:(http://apiserver)" \
            ":role name:(ROLE,version)"
          ;;
        install)
          _arguments \
            "(-h --help)"{-h,--help}"[help message]" \
            "(-i --ignore-errors)"{-i,--ignore-errors}"[Ignore errors and continue with the next specified role]" \
            "(-n --no-deps)"{-n,--no-deps}"[Don't download roles listed as dependencies]" \
            "(-r --role-file)"{-r,--role-file}"[ROLE_FILE A file containing a list of roles to be imported]:role file:_files" \
            "(-p --roles-path)"{-p,--roles-path}"[ROLES_PATH The path to the directory containing your roles (default: from ansible.cfg)]:roles path:_files -/" \
            "(-s --server)"{-s,--server}"[API_SERVER The API server destination]:api server:(http://apiserver)" \
            "(-f --force)"{-f,--force}"[Force overwriting an existing role]" \
            ":role name:(ROLE)"
          ;;
        list)
          _arguments \
            "(-h --help)"{-h,--help}"[help message]" \
            "(-p --roles-path)"{-p,--roles-path}"[ROLES_PATH The path to the directory containing your roles (default: from ansible.cfg)]:roles path:_files -/" \
            ":role name:(ROLE)"
          ;;
        remove)
          _arguments \
            "(-h --help)"{-h,--help}"[help message]" \
            "(-p --roles-path)"{-p,--roles-path}"[ROLES_PATH The path to the directory containing your roles (default: from ansible.cfg)]:roles path:_files -/" \
            "*:role name:(ROLE)"
          ;;
      esac
      ;;
  esac
}

_ansible-galaxy "$@"

# Local Variables:
# mode: Shell-Script
# sh-indentation: 2
# indent-tabs-mode: nil
# sh-basic-offset: 2
# End:
# vim: ft=zsh sw=2 ts=2 et
